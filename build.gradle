//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
plugins {
    id "maven-publish"
    alias libs.plugins.loom
}

version = "$project.baseVersion+$project.branch"
group = "dev.hephaestus"

repositories {
    maven { url "https://maven.nucleoid.xyz/" }
}

dependencies {
    minecraft libs.mc
    mappings variantOf(libs.yarn) { classifier "v2" }
    modImplementation libs.fl
    modImplementation libs.fapi
    modImplementation libs.placeholder
    modCompileOnly libs.polydex

    include libs.placeholder
    modLocalRuntime libs.polydex
}

loom {
    accessWidenerPath = file("src/main/resources/glowcase.accesswidener")
}

processResources {
    final Map<String, String> map = [
            "version"    : version.toString(),
            "mc"         : libs.versions.mc.get(),
            "fl"         : libs.versions.fl.get(),
            "fapi"       : libs.versions.fapi.get(),
            "placeholder": libs.versions.placeholder.get(),
            "polydex"    : libs.versions.polydex.get()
    ]

    inputs.properties(map)
    filesMatching("*.mod.json") { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

java {
    withSourcesJar()
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
